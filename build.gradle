buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 25
        targetSdkVersion 29
        versionCode 5
        versionName "5prealpha"
        setProperty("archivesBaseName", "pocketsphinx-android-$versionName")
        externalNativeBuild {
            cmake {
            }
        }
            ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }
    sourceSets {
         main {
              java.srcDirs = ['src/main/java', 'build/generated-src']
         }
    }
    externalNativeBuild {
        cmake {
                path "CMakeLists.txt"
        }
    }
}

task mkdir {
    doLast {
        mkdir "build"
        mkdir "build/generated-src/csharp"
        mkdir "build/generated-src/java"
        mkdir "build/generated-src/cpp"
    }
}

def sphinxbase_home = "$System.env.SPHINXBASE_HOME"
def pocketsphinx_home = "$System.env.POCKETSPHINX_HOME"

task swigSbJava(type: Exec) {
    commandLine 'swig',
	"-I$sphinxbase_home/include", "-I$sphinxbase_home/swig",
    "-java", "-package", "edu.cmu.pocketsphinx",
    "-outdir", "build/generated-src/java", "-o", "build/generated-src/cpp/sphinxbase_wrap.c",
	"$sphinxbase_home/swig/sphinxbase.i"
}

task swigSbCsharp(type: Exec) {
    commandLine 'swig',
	"-I$sphinxbase_home/include", "-I$sphinxbase_home/swig",
	"-csharp", "-dllimport", "sphinxbase.dll", "-namespace", "SphinxBase",
	"-outdir", "build/generated-src/csharp", "-o", "build/generated-src/cpp/sphinxbase_cs_wrap.c",
	"$sphinxbase_home/swig/sphinxbase.i"
}

task swigPsJava(type: Exec) {
    commandLine 'swig',
        "-I$sphinxbase_home/swig",
        "-I$pocketsphinx_home/include",
        "-I$pocketsphinx_home/swig",
        "-java", "-package", "edu.cmu.pocketsphinx",
        "-outdir", "build/generated-src/java", "-o", "build/generated-src/cpp/pocketsphinx_wrap.c",
        "$pocketsphinx_home/swig/pocketsphinx.i"
}

task swigPsCsharp(type: Exec) {
    commandLine 'swig',
        "-I$sphinxbase_home/swig",
        "-I$pocketsphinx_home/include",
        "-I$pocketsphinx_home/swig",
        "-csharp", "-dllimport", "pocketsphinx.dll", "-namespace", "PocketSphinx",
        "-outdir", "build/generated-src/csharp", "-o", "build/generated-src/cpp/pocketsphinx_cs_wrap.c",
        "$pocketsphinx_home/swig/pocketsphinx.i"
}

preBuild.dependsOn mkdir, swigSbJava, swigPsJava, swigSbCsharp, swigPsCsharp
